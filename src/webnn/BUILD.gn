# Copyright 2019 The Dawn Authors
# Copyright 2021 The WebNN-native Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("../../scripts/dawn_overrides_with_defaults.gni")

import("${dawn_root}/scripts/dawn_component.gni")
import("${dawn_root}/src/webnn/webnn_generator.gni")

group("all") {
  testonly = true
  deps = [
    "${dawn_root}/src/webnn/webnn_native:webnn_native",
    "${dawn_root}/src/webnn/tests:webnn_tests",
  ]
}

###############################################################################
# WebNN headers
###############################################################################

webnn_json_generator("webnn_headers_gen") {
  target = "dawn_headers"
  outputs = [
    "src/include/dawn/webnn_proc_table.h",
    "src/include/dawn/webnn.h",
  ]
}

webnn_json_generator("webnn_emscripten_bits_gen") {
  target = "emscripten_bits"
  outputs = [
    "src/dawn/webnn_struct_info.json",
    "src/dawn/library_webnn_enum_tables.js",
  ]
}

source_set("webnn_headers") {
  all_dependent_configs =
      [ 
        "${dawn_root}/src/common:dawn_public_include_dirs",
        "${dawn_root}/src/webnn/common:webnn_public_include_dirs" ]
  public_deps = [ ":webnn_headers_gen" ]

  sources = get_target_outputs(":webnn_headers_gen")
}

###############################################################################
# WebNN C++ headers
###############################################################################

webnn_json_generator("webnncpp_headers_gen") {
  target = "dawncpp_headers"
  outputs = [
    "src/include/dawn/webnn_cpp.h",
    "src/include/dawn/webnn_cpp_print.h",
  ]
}

source_set("webnncpp_headers") {
  public_deps = [
    ":webnn_headers",
    ":webnncpp_headers_gen",
  ]

  sources = get_target_outputs(":webnncpp_headers_gen")
  sources += [ "${dawn_root}/src/include/dawn/WebnnEnumClassBitmasks.h" ]
}

###############################################################################
# WebNN C++ wrapper
###############################################################################

webnn_json_generator("webnncpp_gen") {
  target = "dawncpp"
  outputs = [ "src/dawn/webnn_cpp.cpp" ]
}

source_set("webnncpp") {
  deps = [
    ":webnncpp_gen",
    ":webnncpp_headers",
  ]
  sources = get_target_outputs(":webnncpp_gen")
}

###############################################################################
# webnn_proc
###############################################################################

webnn_json_generator("webnn_proc_gen") {
  target = "dawn_proc"
  outputs = [
    "src/dawn/webnn_proc.c",
    "src/dawn/webnn_thread_dispatch_proc.cpp"
  ]
}

dawn_component("webnn_proc") {
  DEFINE_PREFIX = "ML"

  public_deps = [ ":webnn_headers" ]
  deps = [ ":webnn_proc_gen" ]
  sources = get_target_outputs(":webnn_proc_gen")
  sources += [ "${dawn_root}/src/dawn/include/webnn_proc.h" ]
}
